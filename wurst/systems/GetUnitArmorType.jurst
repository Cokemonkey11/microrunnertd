library GetUnitArmorType initializer init
	import StructuredDD
	import Table
	import Players

	globals
		Table tab
		constant integer BONUS_HP = 'A00M'
	end

	public class ArmorType extends array
		static constant integer UNKNOWN = 0
		static constant integer MEDIUM_FORTIFIED = 1
		static constant integer UNARMORED = 2
		static constant integer HERO = 3
		static constant integer DIVINE = 4
		static constant integer HEAVY_NORMAL = 5
		static constant integer LIGHT = 6
	end

	public function getUnitArmorType takes unit u returns integer
		integer typ = GetUnitTypeId(u)
		integer ret
		real hp
		real q

		if tab.hasInt(typ) then
			return tab.loadInt(typ)
		else
			hp = GetWidgetLife(u)
			UnitAddAbility(u, BONUS_HP)
			SetUnitState(u, UNIT_STATE_LIFE, 10000)
			StructuredDD.disable()
			UnitDamageTarget(u, u,1., true, true, ConvertAttackType(7), DAMAGE_TYPE_UNIVERSAL, WEAPON_TYPE_WHOKNOWS)
			StructuredDD.enable()
			q = 10000 - GetWidgetLife(u)
			if q == 1 then
				tab.saveInt(typ, ArmorType.MEDIUM_FORTIFIED)
				ret = ArmorType.MEDIUM_FORTIFIED
			elseif q == 25 then
				tab.saveInt(typ, ArmorType.UNARMORED)
				ret = ArmorType.UNARMORED
			elseif q == 150 then
				tab.saveInt(typ, ArmorType.HERO)
				ret = ArmorType.HERO
			elseif q == 400 then
				tab.saveInt(typ, ArmorType.DIVINE)
				ret = ArmorType.DIVINE
			elseif q == 522 then
				tab.saveInt(typ, ArmorType.HEAVY_NORMAL)
				ret = ArmorType.HEAVY_NORMAL
			elseif q == 2048 then
				tab.saveInt(typ, ArmorType.LIGHT)
				ret = ArmorType.LIGHT
			else
				tab.saveInt(typ, ArmorType.UNKNOWN)
				debug call DisplayTextToPlayer(Players.locl, 0., 0., "Debug GetUnitArmorType: Unknown armor type uncovered for " + GetUnitName(u) + "; q = " +R2S(q))
				ret = ArmorType.UNKNOWN
			end
			UnitRemoveAbility(u, BONUS_HP)
			SetWidgetLife(u, hp)
			return ret
		end
	end

	function init()
		tab = new Table
	end
