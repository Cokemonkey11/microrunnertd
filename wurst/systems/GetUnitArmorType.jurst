library GetUnitArmorType
	import StructuredDD
	import Table
	import Players

	constant tab = new Table
	constant BONUS_HP = 'A00M'

	public class ArmorType extends array
		static constant UNKNOWN = 0
		static constant MEDIUM_FORTIFIED = 1
		static constant UNARMORED = 2
		static constant HERO = 3
		static constant DIVINE = 4
		static constant HEAVY_NORMAL = 5
		static constant LIGHT = 6
	end

	public function getUnitArmorType takes unit u returns int
		int typ = GetUnitTypeId(u)
		int ret
		real hp
		real q

		if tab.hasInt(typ)
			return tab.loadInt(typ)
		else
			hp = GetWidgetLife(u)
			UnitAddAbility(u, BONUS_HP)
			SetUnitState(u, UNIT_STATE_LIFE, 10000)
			StructuredDD.disable()
			UnitDamageTarget(u, u, 1., true, true, ConvertAttackType(7), DAMAGE_TYPE_UNIVERSAL, WEAPON_TYPE_WHOKNOWS)
			StructuredDD.enable()
			q = 10000 - GetWidgetLife(u)
			if q == 1
				tab.saveInt(typ, ArmorType.MEDIUM_FORTIFIED)
				ret = ArmorType.MEDIUM_FORTIFIED
			elseif q == 25
				tab.saveInt(typ, ArmorType.UNARMORED)
				ret = ArmorType.UNARMORED
			elseif q == 150
				tab.saveInt(typ, ArmorType.HERO)
				ret = ArmorType.HERO
			elseif q == 400
				tab.saveInt(typ, ArmorType.DIVINE)
				ret = ArmorType.DIVINE
			elseif q == 522
				tab.saveInt(typ, ArmorType.HEAVY_NORMAL)
				ret = ArmorType.HEAVY_NORMAL
			elseif q == 2048
				tab.saveInt(typ, ArmorType.LIGHT)
				ret = ArmorType.LIGHT
			else
				tab.saveInt(typ, ArmorType.UNKNOWN)
				debug call DisplayTextToPlayer(Players.locl, 0., 0., "Debug GetUnitArmorType: Unknown armor type uncovered for " + GetUnitName(u) + "; q = " +R2S(q))
				ret = ArmorType.UNKNOWN
			end
			UnitRemoveAbility(u, BONUS_HP)
			SetWidgetLife(u, hp)
			return ret
		end
	end
