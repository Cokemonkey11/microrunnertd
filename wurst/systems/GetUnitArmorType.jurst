library GetUnitArmorType initializer init
	import StructuredDD
	import Table
	import Players

	globals
		private Table tab
		private constant integer BONUS_HP = 'A00M'
	end

	struct ArmorType extends array
		public static constant integer UNKNOWN = 0
		public static constant integer MEDIUM_FORTIFIED = 1
		public static constant integer UNARMORED = 2
		public static constant integer HERO = 3
		public static constant integer DIVINE = 4
		public static constant integer HEAVY_NORMAL = 5
		public static constant integer LIGHT = 6
	endstruct

	function GetUnitArmorType takes unit u returns integer
		local integer typ = GetUnitTypeId(u)
		local integer ret = ArmorType.UNKNOWN
		local real hp
		local real q
		if tab.exists(typ) then
			return tab[typ]
		else
			hp = GetWidgetLife(u)
			call UnitAddAbility(u,BONUS_HP)
			call SetUnitState(u,UNIT_STATE_LIFE,10000)
			call StructuredDD.disable()
			call UnitDamageTarget(u,u,1.,true,true,ConvertAttackType(7),DAMAGE_TYPE_UNIVERSAL,WEAPON_TYPE_WHOKNOWS)
			call StructuredDD.enable()
			q = 10000 - GetWidgetLife(u)
			if q == 1 then
				tab[typ] = ArmorType.MEDIUM_FORTIFIED
				ret = ArmorType.MEDIUM_FORTIFIED
			elseif q == 25 then
				tab[typ] = ArmorType.UNARMORED
				ret = ArmorType.UNARMORED
			elseif q == 150 then
				tab[typ] = ArmorType.HERO
				ret = ArmorType.HERO
			elseif q == 400 then
				tab[typ] = ArmorType.DIVINE
				ret = ArmorType.DIVINE
			elseif q == 522 then
				tab[typ] = ArmorType.HEAVY_NORMAL
				ret = ArmorType.HEAVY_NORMAL
			elseif q == 2048 then
				tab[typ] = ArmorType.LIGHT
				ret = ArmorType.LIGHT
			else
				tab[typ] = ArmorType.UNKNOWN
				debug call DisplayTextToPlayer(Players.locl,0.,0.,"Debug GetUnitArmorType: Unknown armor type uncovered for " + GetUnitName(u) + "; q = " +R2S(q))
				ret = ArmorType.UNKNOWN
			endif
			call UnitRemoveAbility(u,BONUS_HP)
			call SetWidgetLife(u,hp)
			return ret
		endif
	end

	private function init takes nothing returns nothing
		tab = Table.create()
	end
end
