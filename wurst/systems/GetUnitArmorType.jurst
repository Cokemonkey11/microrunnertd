library GetUnitArmorType initializer init
	import StructuredDD
	import Table
	import Players

	globals
		private Table tab
		private constant integer BONUS_HP = 'A00M'
	end

	struct ArmorType extends array
		public static constant integer UNKNOWN = 0
		public static constant integer MEDIUM_FORTIFIED = 1
		public static constant integer UNARMORED = 2
		public static constant integer HERO = 3
		public static constant integer DIVINE = 4
		public static constant integer HEAVY_NORMAL = 5
		public static constant integer LIGHT = 6
	endstruct

	function GetUnitArmorType takes unit u returns integer
		integer typ = GetUnitTypeId(u)
		integer ret = ArmorType.UNKNOWN
		real hp
		real q

		if tab.hasInt(typ) then
			return tab.loadInt(typ)
		else
			hp = GetWidgetLife(u)
			UnitAddAbility(u,BONUS_HP)
			SetUnitState(u,UNIT_STATE_LIFE,10000)
			StructuredDD.disable()
			UnitDamageTarget(u,u,1.,true,true,ConvertAttackType(7),DAMAGE_TYPE_UNIVERSAL,WEAPON_TYPE_WHOKNOWS)
			StructuredDD.enable()
			q = 10000 - GetWidgetLife(u)
			if q == 1 then
				tab.saveInt(typ, ArmorType.MEDIUM_FORTIFIED)
				ret = ArmorType.MEDIUM_FORTIFIED
			elseif q == 25 then
				tab.saveInt(typ, ArmorType.UNARMORED)
				ret = ArmorType.UNARMORED
			elseif q == 150 then
				tab.saveInt(typ, ArmorType.HERO)
				ret = ArmorType.HERO
			elseif q == 400 then
				tab.saveInt(typ, ArmorType.DIVINE)
				ret = ArmorType.DIVINE
			elseif q == 522 then
				tab.saveInt(typ, ArmorType.HEAVY_NORMAL)
				ret = ArmorType.HEAVY_NORMAL
			elseif q == 2048 then
				tab.saveInt(typ, ArmorType.LIGHT)
				ret = ArmorType.LIGHT
			else
				tab.saveInt(typ, ArmorType.UNKNOWN)
				debug call DisplayTextToPlayer(Players.locl,0.,0.,"Debug GetUnitArmorType: Unknown armor type uncovered for " + GetUnitName(u) + "; q = " +R2S(q))
				ret = ArmorType.UNKNOWN
			endif
			UnitRemoveAbility(u,BONUS_HP)
			SetWidgetLife(u,hp)
			return ret
		endif
	end

	private function init takes nothing returns nothing
		tab = new Table
	end
