library Game initializer init requires Players, Units, Sound
	globals
		private constant real ROUND_DELAY = 20.
		private constant real ROUND_DELAY_HARD = 10.
		private constant real SPAWN_DELAY = 3.
		private constant integer SPAWN_SIZE = 10
		private constant integer GOLD_START = 40
		private constant integer GOLD_BASE = 8
		private constant integer GOLD_PER_LEVEL = 2
		
		
		
		private constant integer DAMAGE_BONUS = 'A00H'
		private constant integer MOVE_SPEED_BONUS = 'A00U'
		
		private constant integer GRAVITY_ABILITY = 'A006'
		
		private constant string TELE_FX = "Abilities\\Spells\\Human\\MassTeleport\\MassTeleportCaster.mdl"
		private constant string TELE_IN_FX = "Abilities\\Spells\\Human\\MassTeleport\\MassTeleportTarget.mdl"
		private constant string TELE_BIG_FX = "Abilities\\Spells\\Human\\MassTeleport\\MassTeleportTo.mdl"
		private constant string BLINK_FX = "Abilities\\Spells\\NightElf\\Blink\\BlinkTarget.mdl"
		
		private constant string RUN1 = "run1.wav"
		private constant string RUN2 = "run2.wav"
		private constant string RUN3 = "run3.wav"
		private constant string RUN4 = "scar-clip-15db.wav"
		private constant string RUN5 = "csgo_st6_toolaterun.wav"
		
		private constant string BANDIT_DEMO = "Units\\Creeps\\Bandit\\BanditYesAttack3.wav"
		private constant string BANDIT = "Units\\Creeps\\Bandit\\BanditYesAttack2.wav"
		private constant string KING   = "Units\\Human\\HeroMountainKing\\HeroMountainKingPissed7.wav"
		private constant string QUILL  = "Units\\Creeps\\QuillBeast\\QuillBoarReady1.wav"
		private constant string CHIEF  = "Units\\Orc\\Cairne\\CairnePissed1.wav"
		private constant string LORD   = "Units\\Demon\\HeroPitLord\\HPitLordPissed4.wav"
		private constant string VICTORY = "Sound\\Interface\\ClanInvitation.wav"
		private constant string BEGIN = "Sound\\Interface\\QuestLog.wav"
		private constant string TIP    = "Sound\\Interface\\SecretFound.wav"
		
		private effect bigFx
		
		public boolean ended = false
		public boolean started = false
		public boolean hard = false
		public boolean array upgraded[2]
		public integer round
		private timer roundTimer = CreateTimer()
		private timerdialog roundDialog
		
		public timer chooseTimer = CreateTimer()
		public timerdialog chooseDialog
		
		private unit demoRunner
		private unit demoChaser
		private unit demoTower
		
		private camerasetup setup
		
		private group grp = CreateGroup()
	endglobals
	
	public function fadeOut takes real duration returns nothing
		call EnableUserUI(false)
		call SetCineFilterTexture("ReplaceableTextures\\CameraMasks\\White_mask.blp")
		call SetCineFilterBlendMode(BLEND_MODE_BLEND)
		call SetCineFilterTexMapFlags(TEXMAP_FLAG_NONE)
		call SetCineFilterStartUV(0, 0, 1, 1)
		call SetCineFilterEndUV(0, 0, 1, 1)
		call SetCineFilterStartColor(0, 0, 0, 0)
		call SetCineFilterEndColor(0, 0, 0, 255)
		call SetCineFilterDuration(duration)
		call DisplayCineFilter(true)
	endfunction
	
	public function fadeIn takes real duration returns nothing
		call EnableUserUI(true)
		call SetCineFilterTexture("ReplaceableTextures\\CameraMasks\\White_mask.blp")
		call SetCineFilterBlendMode(BLEND_MODE_BLEND)
		call SetCineFilterTexMapFlags(TEXMAP_FLAG_NONE)
		call SetCineFilterStartUV(0, 0, 1, 1)
		call SetCineFilterEndUV(0, 0, 1, 1)
		call SetCineFilterStartColor(0, 0, 0, 255)
		call SetCineFilterEndColor(0, 0, 0, 0)
		call SetCineFilterDuration(duration)
		call DisplayCineFilter(true)
	endfunction
	
	private function afterEnd takes nothing returns nothing
		call DestroyTimer(GetExpiredTimer())
		call EndGame(true)
	endfunction
	
	private function fadeWait takes nothing returns nothing
		call DestroyTimer(GetExpiredTimer())
		call fadeOut(10.)
	endfunction
	
	public function end takes nothing returns nothing
		call Units_freezeAll()
		set ended = true
		call TimerStart(CreateTimer(),20.,false,function fadeWait)
		call TimerStart(CreateTimer(),30.,false,function afterEnd)
	endfunction
	
	private function hint0 takes nothing returns nothing
		call DestroyTimer(GetExpiredTimer())
		call DisplayTextToPlayer(Players_locl,0.,0.,"|cffffcc00Tip:|r You can upgrade various runner improvements at your altar.")
		call Sound.play(TIP,1.,127)
	endfunction
	
	private function hint1 takes nothing returns nothing
		call DestroyTimer(GetExpiredTimer())
		call DisplayTextToPlayer(Players_locl,0.,0.,"|cffffcc00Tip:|r If both players type |cffffcc00-hard|r before the practice round begins, the game becomes more difficult.")
		call Sound.play(TIP,1.,127)
	endfunction
	
	private function spawn takes nothing returns nothing
		local integer index = 0
		local integer size
		local unit u
		call DestroyEffect(bigFx)
		call Sound.play(BEGIN,1.,127)
		if round == 0 then
			call DisplayTextToPlayer(Players_locl,0.,0.,"|cffffcc00Practice Round|r")
		else
			call DisplayTextToPlayer(Players_locl,0.,0.,"|cffffcc00Round: |r"+I2S(round))
		endif
		
		// Display upgrade tip only if neither player has upgraded already
		if round == 10 and ( (not (upgraded[0] or upgraded[1]) or (not upgraded[0] and Players_playing[0]) or (not upgraded[1] and Players_playing[1])) ) then
			call TimerStart(CreateTimer(),5.,false,function hint0)
		endif
		
		// Display hard mode tip only in co-operative normal mode
		if round == 20 and not Game_hard and Players_playingCount > 1 then
			call TimerStart(CreateTimer(),5.,false,function hint1)
		endif
		set size = SPAWN_SIZE
		if ModuloInteger(round,6) == 0 then
			// Hero Round
			if (round != 0) then
				call DisplayTextToPlayer(Players_locl,0.,0.,"|cffffcc00Boss Round!|r")
			endif
			if (round == 6) then
				call Sound.play(BANDIT,1.,127)
			endif
			if (round == 12) then
				call Sound.play(KING,1.,127)
			endif
			if (round == 18) then
				call Sound.play(QUILL,1.,127)
			endif
			if (round == 24) then
				call Sound.play(CHIEF,1.,127)
			endif
			if (round == 30) then
				call Sound.play(LORD, 1.,127)
			endif
			set size = 2
		endif
		if ModuloInteger(round,14) == 0 and round !=0 then
			set size = size * 2
			call DisplayTextToPlayer(Players_locl,0.,0.,"|cff222222Swarm Round!|r")
		endif
		if ModuloInteger(round,8) == 0 and round!= 0 then
			// Spell Immune Round
			call DisplayTextToPlayer(Players_locl,0.,0.,"|cffaacceeSpell Immune Round!|r")
		endif
		if ModuloInteger(round,15) == 0 and round !=0 then
			// Gravity Round
			call DisplayTextToPlayer(Players_locl,0.,0.,"|cff999999High Gravity Round!|r")
		endif
		loop
			exitwhen index >= size
			set u = CreateUnit(Players_creeps,Units_spawns[round],Players_brownStartX,Players_brownStartY,180.)
			if Game_hard then
				call UnitAddAbility(u,DAMAGE_BONUS)
				call UnitAddAbility(u,MOVE_SPEED_BONUS)
			endif
			set index = index + 1
		endloop
		set u = null
	endfunction
	
	private function runSound takes nothing returns nothing
		local integer q = ModuloInteger(round,5)
		if q == 0 then
			call Sound.play(RUN1, 1., 127)
		elseif q == 1 then
			call Sound.play(RUN2, 1., 127)
		elseif q == 2 then
			call Sound.play(RUN3, 1., 127)
		elseif q == 3 then
			call Sound.play(RUN4, 1., 127)
		else
			call Sound.play(RUN5, 1., 127)
		endif
	endfunction
	
	private function applyTowerGravity takes nothing returns nothing
		local unit FoG
		call GroupEnumUnitsOfPlayer(grp,Players_id[0],null)
		loop
			set FoG=FirstOfGroup(grp)
			exitwhen FoG==null
			if UnitAlive(FoG) and IsUnitType(FoG,UNIT_TYPE_STRUCTURE) then
				call UnitAddAbility(FoG,'A017')
			endif
			call GroupRemoveUnit(grp,FoG)
		endloop
		call GroupEnumUnitsOfPlayer(grp,Players_id[1],null)
		loop
			set FoG=FirstOfGroup(grp)
			exitwhen FoG==null
			if UnitAlive(FoG) and IsUnitType(FoG,UNIT_TYPE_STRUCTURE) then
				call UnitAddAbility(FoG,'A017')
			endif
			call GroupRemoveUnit(grp,FoG)
		endloop
	endfunction
	
	private function removeTowerGravity takes nothing returns nothing
		local unit FoG
		call GroupEnumUnitsOfPlayer(grp,Players_id[0],null)
		loop
			set FoG=FirstOfGroup(grp)
			exitwhen FoG==null
			if UnitAlive(FoG) and IsUnitType(FoG,UNIT_TYPE_STRUCTURE) then
				call UnitRemoveAbility(FoG,'A017')
			endif
			call GroupRemoveUnit(grp,FoG)
		endloop
		call GroupEnumUnitsOfPlayer(grp,Players_id[1],null)
		loop
			set FoG=FirstOfGroup(grp)
			exitwhen FoG==null
			if UnitAlive(FoG) and IsUnitType(FoG,UNIT_TYPE_STRUCTURE) then
				call UnitRemoveAbility(FoG,'A017')
			endif
			call GroupRemoveUnit(grp,FoG)
		endloop
	endfunction
	
	private function roundBegin takes nothing returns nothing
		local integer index = 0
		call TimerDialogDisplay(roundDialog,false)
		if not ended then
			if round == 0 then
				set started = true
			endif
			set bigFx = AddSpecialEffect(TELE_BIG_FX,Players_brownStartX,Players_brownStartY)
			call runSound()
			call DisplayTextToPlayer(Players_locl,0.,0.,"|cffff0000RUN AWAY|r")
			if ModuloInteger(round,15) == 0 and round != 0 then
				call applyTowerGravity()
			endif
			loop
				exitwhen index > 1
				if Players_playing[index] then
					set Units_runners[index] = CreateUnit(Players_id[index],Units_runnerTypes[index],Players_startX[index],Players_startY[index]-16. + 32. * index,180.)
					if ModuloInteger(round,15) == 0 and round != 0 then
						call UnitAddAbility(Units_runners[index],GRAVITY_ABILITY)
						call Knockback3D.setGravity(90.)
					else
						call Knockback3D.setGravity(45.)
					endif
					call DestroyEffect(AddSpecialEffect(TELE_IN_FX,Players_startX[index],Players_startY[index]))
					if Players_locl == Players_id[index] then
						call ClearSelection()
						call SelectUnit(Units_runners[index],true)
						call PanCameraToTimed(Players_startX[index],Players_startY[index],0.)
					endif
					call UnitAddAbility(Units_runners[index],'Adef')
					call UnitRemoveAbility(Units_runners[index],'Adef')
				endif
				set index = index + 1
			endloop
			call TimerStart(GetExpiredTimer(),SPAWN_DELAY,false,function spawn)
		endif
	endfunction
	
	private function queueRound takes nothing returns nothing
		local integer q
		if Game_hard then
			call TimerStart(roundTimer,ROUND_DELAY_HARD,false,function roundBegin)
		else
			call TimerStart(roundTimer,ROUND_DELAY,false,function roundBegin)
		endif
		call TimerDialogSetTitle(roundDialog,"Round "+I2S(round)+" in")
		call TimerDialogDisplay(roundDialog,true)
	endfunction
	
	public function practiceRound takes nothing returns nothing
		call DestroyTimer(GetExpiredTimer())
		call DisplayTextToPlayer(Players_locl,0.,0.,"\nThe first round will be a |cffffcc00practice|r round to show you how it works!")
		set round = 0
		call queueRound()
	endfunction
	
	public function success takes nothing returns nothing
		local integer index = 0
		local integer val
		set round = round + 1
		if not ended then
			if ModuloInteger(round-1,15) == 0 and round-1 !=0 then
				call removeTowerGravity()
			endif
			loop
				exitwhen index > 1
				if UnitAlive(Units_runners[index]) then
					call DestroyEffect(AddSpecialEffect(TELE_FX,GetUnitX(Units_runners[index]),GetUnitY(Units_runners[index])))
					call RemoveUnit(Units_runners[index])
				endif
				set index = index + 1
			endloop
			if round >= Units_ROUND_COUNT then
				call RemovePlayer(Players_id[0],PLAYER_GAME_RESULT_VICTORY)
				call RemovePlayer(Players_id[1],PLAYER_GAME_RESULT_VICTORY)
				call DisplayTextToPlayer(Players_locl,0.,0.,"You won every round, |cffffcc00congratulations|r! The game will end in 30 seconds.")
				call Sound.play(VICTORY,1.,127)
				call end()
			else
				if round == 1 then
					call DisplayTextToPlayer(Players_locl,0.,0.,"|cffffcc00Great job!|r The first round will begin momentarily.")
				else
					set val = (GOLD_BASE + GOLD_PER_LEVEL * (round-1)) / Players_playingCount
					call DisplayTextToPlayer(Players_locl,0.,0.,"|cffffcc00Success!|r You received |cffffcc00" + I2S(val) + /*
						*/ "|r bonus gold for completing |cffffcc00round " + I2S(round-1) + "|r. The next round will begin momentarily!")
					call SetPlayerState(Players_id[0],PLAYER_STATE_RESOURCE_GOLD,GetPlayerState(Players_id[0],PLAYER_STATE_RESOURCE_GOLD) + val)
					call SetPlayerState(Players_id[1],PLAYER_STATE_RESOURCE_GOLD,GetPlayerState(Players_id[1],PLAYER_STATE_RESOURCE_GOLD) + val)
				endif
				call queueRound()
			endif
		endif
	endfunction
	
	private function fadeCameraAsync1 takes nothing returns nothing
		call DestroyTimer(GetExpiredTimer())
		call CameraSetupApply(setup,true,true)
		call fadeIn(.25)
	endfunction
	
	private function fadeCameraAsync takes camerasetup q returns nothing
		set setup = q
		call fadeOut(.25)
		call TimerStart(CreateTimer(),.25,false,function fadeCameraAsync1)
	endfunction
	
	private function tip0 takes nothing returns nothing
		call DestroyTimer(GetExpiredTimer())
		call DisplayTextToPlayer(Players_locl,0.,0.,"|cffffcc00Tip:|r There are |cffffcc00Boss|r, " + /*
			*/ "|cffaacceeSpell Immune|r, |cff222222Swarm|r, and |cff999999High Gravity|r Rounds.")
		call Sound.play(TIP,1.,127)
	endfunction
	
	private function afterChoose takes nothing returns nothing
		local integer r
		local unit u
		if Players_playing[0] and Units_runnerTypes[0]==0 then
			set r = GetRandomInt(0,1)
			if r == 0 then
				set u = CreateUnit(Players_id[0],Units_SPIRIT_ID,GetUnitX(Units_choosers[0]),GetUnitY(Units_choosers[0]),270.)
				call DisplayTextToPlayer(Players_id[0],0.,0.,"You took too long to choose, and have randomed |cffffcc00Spirit|r.")
				if Players_locl == Players_id[0] then
					call SelectUnit(u,true)
				endif
				set Units_runnerTypes[0] = Units_RUNNER_ID
				call CreateUnit(Players_id[0],Units_SPIRIT_ALTAR_ID,Players_altarX[0],Players_altarY[0],270.)
			else
				set u = CreateUnit(Players_id[0],Units_WASTE_ID,GetUnitX(Units_choosers[0]),GetUnitY(Units_choosers[0]),270.)
				call DisplayTextToPlayer(Players_id[0],0.,0.,"You took too long to choose, and have randomed |cffffcc00Wasteland|r.")
				if Players_locl == Players_id[0] then
					call SelectUnit(u,true)
				endif
				set Units_runnerTypes[0] = Units_RUNNER_WASTE_ID
				call CreateUnit(Players_id[0],Units_WASTE_ALTAR_ID,Players_altarX[0],Players_altarY[0],270.)
			endif
			call RemoveUnit(Units_choosers[0])
		endif
		if Players_playing[1] and Units_runnerTypes[1]==0 then
			set r = GetRandomInt(0,1)
			if r == 0 then
				set u = CreateUnit(Players_id[1],Units_SPIRIT_ID,GetUnitX(Units_choosers[1]),GetUnitY(Units_choosers[1]),270.)
				call DisplayTextToPlayer(Players_id[1],0.,0.,"You took too long to choose, and have randomed |cffffcc00Spirit|r.")
				if Players_locl == Players_id[1] then
					call SelectUnit(u,true)
				endif
				set Units_runnerTypes[1] = Units_RUNNER_ID
				call CreateUnit(Players_id[1],Units_SPIRIT_ALTAR_ID,Players_altarX[1],Players_altarY[0],270.)
			else
				set u = CreateUnit(Players_id[1],Units_WASTE_ID,GetUnitX(Units_choosers[1]),GetUnitY(Units_choosers[1]),270.)
				call DisplayTextToPlayer(Players_id[1],0.,0.,"You took too long to choose, and have randomed |cffffcc00Wasteland|r.")
				if Players_locl == Players_id[1] then
					call SelectUnit(u,true)
				endif
				set Units_runnerTypes[1] = Units_RUNNER_WASTE_ID
				call CreateUnit(Players_id[1],Units_WASTE_ALTAR_ID,Players_altarX[1],Players_altarY[1],270.)
			endif
			call RemoveUnit(Units_choosers[1])
		endif
		call DestroyTimer(GetExpiredTimer())
		call DestroyTimerDialog(chooseDialog)
		call TimerStart(CreateTimer(),15.,false,function practiceRound)
		set u = null
	endfunction
	
	private function cinematicSequence3 takes nothing returns nothing
		call DestroyTimer(GetExpiredTimer())
		call ShowInterface(true,.5)
		call fadeCameraAsync(gg_cam_defaultCamera)
		call EnableUserControl(true)
		call RemoveUnit(demoRunner)
		call RemoveUnit(demoTower)
		call RemoveUnit(demoChaser)
		set chooseDialog = CreateTimerDialog(chooseTimer)
		call TimerStart(chooseTimer,15.,false,function afterChoose)
		call TimerDialogSetTitle(chooseDialog,"Race select")
		call TimerDialogDisplay(chooseDialog,true)
		set roundDialog = CreateTimerDialog(roundTimer)
		call TimerStart(CreateTimer(),24.,false,function tip0)
		call DisplayTextToPlayer(Players_locl,0.,0.,"\n|cffffcc00Tip:|r You have |cffffcc0015|r seconds to choose a race.")
		call Sound.play(TIP,1.,127)
	endfunction
	
	private function cinematicSequence2 takes nothing returns nothing
		call DestroyTimer(GetExpiredTimer())
		call fadeCameraAsync(gg_cam_cinematicCamera2)
		call DisplayTextToPlayer(Players_locl,0.,0.,"\nBuild towers on the |cff33cc66grass|r, and bait the enemy to their death.")
		call TimerStart(CreateTimer(),5.,false,function cinematicSequence3)
	endfunction
	
	private function cinematicSequence1 takes nothing returns nothing
		call DestroyTimer(GetExpiredTimer())
		call fadeIn(.5)
		set demoRunner = CreateUnit(Player(2),Units_RUNNER_ID,0.,1024.,180.)
		set demoTower = CreateUnit(Player(2),'h007',-851.,562.,270.)
		set demoChaser = CreateUnit(Players_creeps,'h022',256. + 128.,1024.,0.)
		call SetUnitState(demoTower,UNIT_STATE_MANA,500.)
		call SetUnitMoveSpeed(demoRunner,200.)
		call SetUnitMoveSpeed(demoChaser,230.)
		call CameraSetupApply(gg_cam_cinematicCamera1,true,true)
		call IssueTargetOrder(demoChaser,"attack",demoRunner)
		call IssuePointOrder(demoRunner,"move",-1024.,0.)
		call Sound.play(BANDIT_DEMO,1.,127)
		call DisplayTextToPlayer(Players_locl,0.,0.,"\nThe forces of |cff993333Doom|r are after your runner!")
		call TimerStart(CreateTimer(),3.5,false,function cinematicSequence2)
	endfunction
	
	private function cinematicSequence0 takes nothing returns nothing
		call fadeOut(.01)
		call DestroyTimer(GetExpiredTimer())
		call ShowInterface(false, 0.)
        call EnableUserControl(false)
		call TimerStart(CreateTimer(),1.,false,function cinematicSequence1)
	endfunction
	
	private function init takes nothing returns nothing
		local integer index = 0
		
		// Iterate through players
		set index = 0
		loop
			exitwhen index > 1
			if Players_playing[index] then
			
				// Spawn Starting Units
				set Units_choosers[index] = CreateUnit(Players_id[index],Units_RACE_SELECT,Players_startX[index],Players_startY[index],270.)
				if Players_locl == Players_id[index] then
					call SelectUnit(Units_choosers[index],true)
				endif
				
				// Adjust Starting Gold
				call SetPlayerState(Players_id[index],PLAYER_STATE_RESOURCE_GOLD,GOLD_START / Players_playingCount)
			endif
			set index = index + 1
		endloop
		
		// Adjust Fog
		call FogEnable(false)
		call FogMaskEnable(false)
		
		// Messages
		call TimerStart(CreateTimer(),0.,false,function cinematicSequence0)
		
		// Preload runaway sounds
		call Sound.preload(RUN1)
		call Sound.preload(RUN2)
		call Sound.preload(RUN3)
		call Sound.preload(RUN4)
		call Sound.preload(RUN5)
		
		// Preload boss sounds
		call Sound.preload(BANDIT_DEMO)
		call Sound.preload(BANDIT)
		call Sound.preload(KING)
		call Sound.preload(QUILL)
		call Sound.preload(CHIEF)
		call Sound.preload(LORD)
		
		// Preload event sounds
		call Sound.preload(VICTORY)
		call Sound.preload(BEGIN)
		call Sound.preload(TIP)
		
		// Lock time of day
		call SetTimeOfDayScale(0.)
	endfunction
endlibrary
