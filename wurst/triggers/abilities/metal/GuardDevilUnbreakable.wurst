package GuardDevilUnbreakable
import DamageType
import DamageDetection2
import ClosureTimers
import Players
import Invulnerable

constant ID_GUARD_DEVIL         = 'h03B'
constant ID_GUARD_DEVIL_GREATER = 'h03C'
constant ID_ABILITY_UNBREAKABLE = 'A01W'
constant ID_ABILITY_UNBREAKA_CD = 'A01X'
constant ID_INVULNERABLE        = 'Avul'

constant RADIUS   = 600.
constant DURATION =   1.
constant COOLDOWN =  60.

constant SHIELD_FX = "Abilities\\Spells\\Human\\DivineShield\\DivineShieldTarget.mdl"

constant grp = CreateGroup()

function isGuardDevil(unit u) returns bool
	return u.getTypeId() == ID_GUARD_DEVIL or u.getTypeId() == ID_GUARD_DEVIL_GREATER

function findUnbreakable(unit u) returns unit
	unit guard = null

	grp.enumUnitsInRange(vec2(u.getX(), u.getY()), RADIUS)
	for iter from grp
		if iter.isAlive() and isGuardDevil(iter) and iter.hasAbility(ID_ABILITY_UNBREAKABLE)
			guard = iter

	return guard

function h()
	let tU     = GetTriggerUnit()
	let damage = GetEventDamage()

	if tU.getOwner().getId() < Players.COUNT and getDamageType() == DamageType.ATTACK and (tU.getHP() - damage < 1.) and not tU.hasAbility(ID_INVULNERABLE)
		let unbreakableSource = findUnbreakable(tU)

		if unbreakableSource != null
			Invulnerable.apply(tU, DURATION)
			unbreakableSource.removeAbility(ID_ABILITY_UNBREAKABLE)
			unbreakableSource.addAbility(   ID_ABILITY_UNBREAKA_CD)
			let shield = tU.addEffect(SHIELD_FX, "origin")

			doAfter(DURATION, () -> begin
				shield.destr()
			end)

			doAfter(COOLDOWN, () -> begin
				unbreakableSource.addAbility(   ID_ABILITY_UNBREAKABLE)
				unbreakableSource.removeAbility(ID_ABILITY_UNBREAKA_CD)
			end)

init
	addOnDamageFunc(Condition(function h))
