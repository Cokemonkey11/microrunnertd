package GuardDevilUnbreakable
	import DamageType
	import StructuredDD
	import ClosureForGroups
	import ClosureTimers
	import Players

	constant int ID_GUARD_DEVIL         = 'h03B'
	constant int ID_ABILITY_UNBREAKABLE = 'A01W'
	constant int ID_ABILITY_UNBREAKA_CD = 'A01X'
	constant int ID_INVULNERABLE        = 'Avul'

	constant real RADIUS   = 600.
	constant real DURATION =   1.
	constant real COOLDOWN =  60.

	constant string SHIELD_FX = "Abilities\\Spells\\Human\\DivineShield\\DivineShieldTarget.mdl"

	unit guard

	function findUnbreakable(unit u) returns unit
		guard = null

		forUnitsInRange(vec2(u.getX(), u.getY()), RADIUS, (unit iter) -> begin
			if iter.getTypeId() == ID_GUARD_DEVIL and iter.hasAbility(ID_ABILITY_UNBREAKABLE)
				guard = iter
			end
		end)

		return guard
	end

	function h()
		unit dS     = GetEventDamageSource()
		unit tU     = GetTriggerUnit()
		real damage = GetEventDamage()

		if tU.getOwner().getId() < Players.COUNT and DamageType.get() == DamageType.ATTACK and (tU.getHP() - damage < 1.) and not tU.hasAbility(ID_INVULNERABLE)
			unit unbreakableSource = findUnbreakable(tU)

			if unbreakableSource != null
				tU.addAbility(ID_INVULNERABLE)
				// tU.setHP(damage + 1.)
				unbreakableSource.removeAbility(ID_ABILITY_UNBREAKABLE)
				unbreakableSource.addAbility(   ID_ABILITY_UNBREAKA_CD)
				effect shield = tU.addEffect(SHIELD_FX, "origin")

				doAfter(DURATION, () -> begin
					tU.removeAbility(ID_INVULNERABLE)
					shield.destr()

				end)

				doAfter(COOLDOWN, () -> begin
					unbreakableSource.addAbility(   ID_ABILITY_UNBREAKABLE)
					unbreakableSource.removeAbility(ID_ABILITY_UNBREAKA_CD)
				end)
			end
		end
	end

	init
		StructuredDD.addHandler(function h)
	end
