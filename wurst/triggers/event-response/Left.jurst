scope Left initializer init
	globals
		private constant string LIGHTNING = "Doodads\\Cinematic\\Lightningbolt\\LightningBolt1.wav"
	endglobals
	
	private function cond takes nothing returns boolean
		local integer id
		local integer oid
		local group grp
		local unit FoG
		local player p
		local integer q
		if Players.playingCount > 1 then
			set p = GetTriggerPlayer()
			set id = GetPlayerId(p)
			set oid = 1 - id
			set grp = CreateGroup()
			set Players.playing[id] = false
			set Players.playingCount = Players.playingCount - 1
			set q = GetPlayerState(p,PLAYER_STATE_RESOURCE_GOLD)
			call SetPlayerState(p,PLAYER_STATE_RESOURCE_GOLD,0)
			call SetPlayerState(Players.id[oid],PLAYER_STATE_RESOURCE_GOLD,GetPlayerState(Players.id[oid],PLAYER_STATE_RESOURCE_GOLD) + q)
			call GroupEnumUnitsOfPlayer(grp,Players.id[id],null)
			loop	
				set FoG = FirstOfGroup(grp)
				exitwhen FoG== null
				if GetUnitTypeId(FoG) == Units_RACE_SELECT then
					call RemoveUnit(FoG)
				else
					call SetUnitOwner(FoG,Players.id[oid],false)
				endif
				call GroupRemoveUnit(grp,FoG)
			endloop
			if not Game_ended then
				call DisplayTextToPlayer(Players.locl,0.,0.,"\n|cffffcc00" + GetPlayerName(p) + /*
					*/ "|r has left the game. You received |cffffcc00" + I2S(q) + "|r gold.")
				call Sound.play(LIGHTNING,1.,127)
			else
				call DisplayTextToPlayer(Players.locl,0.,0.,"\n|cffffcc00" + GetPlayerName(p) + /*
					*/ "|r has left the game.")
			endif
		endif
		return false
	end
	
	private function init takes nothing returns nothing
		local trigger t = CreateTrigger()
		call TriggerRegisterPlayerEvent(t,Players.id[0],EVENT_PLAYER_LEAVE)
		call TriggerRegisterPlayerEvent(t,Players.id[1],EVENT_PLAYER_LEAVE)
		call TriggerAddCondition(t,Condition(function cond))
		
		call Sound.preload(LIGHTNING)
		set t = null
	end
endscope
