package Left
import Players
import UnitApi
import Game
import Sounds

constant LIGHTNING = "Doodads\\Cinematic\\Lightningbolt\\LightningBolt1.wav"

constant grp = CreateGroup()

function cond() returns boolean
	if Players.playingCount > 1
		let p = GetTriggerPlayer()
		let id = GetPlayerId(p)
		let oid = 1 - id
		Players.playing[id] = false
		Players.playingCount--
		let q = GetPlayerState(p, PLAYER_STATE_RESOURCE_GOLD)
		SetPlayerState(p, PLAYER_STATE_RESOURCE_GOLD, 0)
		SetPlayerState(Players.id[oid], PLAYER_STATE_RESOURCE_GOLD, GetPlayerState(Players.id[oid], PLAYER_STATE_RESOURCE_GOLD) + q)

		GroupEnumUnitsOfPlayer(grp, Players.id[id], null)
		for fst from grp
			if GetUnitTypeId(fst) == UnitApi.RACE_SELECT
				RemoveUnit(fst)
			else
				SetUnitOwner(fst, Players.id[oid], false)

		if not Game.ended
			DisplayTextToPlayer(Players.locl, 0., 0., "\n|cffffcc00" + GetPlayerName(p) + /*
				*/ "|r has left the game. You received |cffffcc00" + I2S(q) + "|r gold.")
			Sounds.play(LIGHTNING, 1., 127)
		else
			DisplayTextToPlayer(Players.locl, 0., 0., "\n|cffffcc00" + GetPlayerName(p) + /*
				*/ "|r has left the game.")
	return false

init
	let t = CreateTrigger()
	TriggerRegisterPlayerEvent(t, Players.id[0], EVENT_PLAYER_LEAVE)
	TriggerRegisterPlayerEvent(t, Players.id[1], EVENT_PLAYER_LEAVE)
	TriggerAddCondition(t, Condition(function cond))

	Sounds.preload(LIGHTNING)
